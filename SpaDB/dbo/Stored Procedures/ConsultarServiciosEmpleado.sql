CREATE PROCEDURE ConsultarServiciosEmpleado(
	@JsonAgenda NVARCHAR(MAX)
)
AS
BEGIN

	DECLARE @IdEmpleado INT	
	DECLARE @FechaDesde DATE
	DECLARE @FechaHasta DATE
	DECLARE @IdEmpresa VARCHAR(36)	

	DECLARE @statement NVARCHAR(MAX) = N'';

	CREATE TABLE #TempBusquedaAgenda (Id_Empleado INT, Fecha_Desde DATETIME, Fecha_Hasta DATETIME, Id_Empresa UNIQUEIDENTIFIER)

	CREATE TABLE #TempServicios (Id_Servicio INT)

	INSERT INTO #TempServicios (Id_Servicio)
	SELECT
		Id_Servicio
	FROM
		OPENJSON(@JsonAgenda, '$')
	WITH (
		Id_Servicios NVARCHAR(MAX) '$.Id_Servicios' AS JSON
	) AS Servicios 
	CROSS APPLY OPENJSON (Servicios.Id_Servicios) WITH (Id_Servicio INT '$')


	INSERT INTO #TempBusquedaAgenda (Id_Empleado, Fecha_Desde, Fecha_Hasta, Id_Empresa)
	SELECT
		Id_Empleado, Fecha_Desde, Fecha_Hasta, Id_Empresa
	FROM
		OPENJSON(@JsonAgenda)
	WITH (
		Id_Empleado INT '$.Id_Empleado', Fecha_Desde DATETIME '$.Fecha_Desde',
		Fecha_Hasta DATETIME '$.Fecha_Hasta', Id_Empresa UNIQUEIDENTIFIER '$.Id_Empresa'		
	)		
	
	SET @IdEmpleado = (SELECT TOP 1 Id_Empleado FROM #TempBusquedaAgenda)	
	SET @FechaDesde = (SELECT TOP 1 CAST(Fecha_Desde AS DATE) FROM #TempBusquedaAgenda)
	SET @FechaHasta = (SELECT TOP 1 CAST(Fecha_Hasta AS DATE) FROM #TempBusquedaAgenda)
	SET @IdEmpresa = (SELECT TOP 1 Id_Empresa FROM #TempBusquedaAgenda)	


	SET @statement = @statement + N' 
	SELECT ID_AGENDA,
		RTRIM(LTRIM(LEFT(RIGHT(CONVERT(VARCHAR(20), FECHA_INICIO, 100), 7),5) + '' '' + RIGHT(CONVERT(VARCHAR(20), FECHA_INICIO, 100), 2))) AS FECHAINICIO,		 
		RTRIM(LTRIM(LEFT(RIGHT(CONVERT(VARCHAR(20), FECHA_FIN, 100), 7),5) + '' '' + RIGHT(CONVERT(VARCHAR(20), FECHA_FIN, 100), 2))) AS FECHAFIN,
		CONVERT(VARCHAR(10), FECHA_INICIO, 105) AS FECHACITA,
		FECHA_INICIO,
		FECHA_FIN,
		EMPRESA_SERVICIOS.VALOR AS VALOR_SERVICIO,
		AGENDA.ID_CLIENTE AS ID_CLIENTE, 
		AGENDA.ID_SERVICIO AS ID_SERVICIO, 
		AGENDA.ID_EMPLEADO AS ID_EMPLEADO,
		RTRIM(EMPLEADOS.NOMBRES) AS NOMBRES_EMPLEADO,
		RTRIM(EMPLEADOS.APELLIDOS) AS APELLIDOS_EMPLEADO,
		RTRIM(CLIENTES.NOMBRES) AS NOMBRES_CLIENTE,
		RTRIM(CLIENTES.APELLIDOS) AS APELLIDOS_CLIENTE,
		RTRIM(CLIENTES.TELEFONO_FIJO) AS TELEFONO_FIJO_CLIENTE,
		RTRIM(CLIENTES.TELEFONO_MOVIL) AS TELEFONO_MOVIL_CLIENTE,
		RTRIM(CLIENTES.MAIL) AS MAIL_CLIENTE,
		RTRIM(AGENDA.ESTADO) AS ESTADO, 
		RTRIM(AGENDA.ID_EMPRESA) AS ID_EMPRESA,
		RTRIM(EMPRESA.NOMBRE) AS NOMBRE_EMPRESA,
		AGENDA.FECHA_REGISTRO AS FECHA_REGISTRO, 
		AGENDA.FECHA_MODIFICACION AS FECHA_MODIFICACION, 
		RTRIM(OBSERVACIONES) AS OBSERVACIONES, 
		CONCAT(RTRIM(LEFT(CLIENTES.NOMBRES,(PATINDEX(''% %'',CLIENTES.NOMBRES)))),'' '' ,RTRIM(LEFT(CLIENTES.APELLIDOS,(PATINDEX(''% %'',CLIENTES.APELLIDOS))))) AS NOMBREAPELLIDO_CLIENTE, 
		CONCAT(RTRIM(LEFT(EMPLEADOS.NOMBRES,(PATINDEX(''% %'',EMPLEADOS.NOMBRES)))),'' '' ,RTRIM(LEFT(EMPLEADOS.APELLIDOS,(PATINDEX(''% %'',EMPLEADOS.APELLIDOS))))) AS NOMBREAPELLIDO_EMPLEADO, 
		RTRIM(SERVICIOS.NOMBRE) AS NOMBRE_SERVICIO
	FROM AGENDA
		INNER JOIN CLIENTES ON CLIENTES.ID_CLIENTE = AGENDA.ID_CLIENTE
		INNER JOIN EMPLEADOS ON EMPLEADOS.ID_EMPLEADO = AGENDA.ID_EMPLEADO
		INNER JOIN SERVICIOS ON SERVICIOS.ID_SERVICIO = AGENDA.ID_SERVICIO
		INNER JOIN EMPRESA ON EMPRESA.ID_EMPRESA = AGENDA.ID_EMPRESA
		INNER JOIN EMPRESA_SERVICIOS ON EMPRESA_SERVICIOS.ID_SERVICIO = AGENDA.ID_SERVICIO AND EMPRESA_SERVICIOS.ID_EMPRESA = AGENDA.ID_EMPRESA
	WHERE (CONVERT(VARCHAR(10),FECHA_INICIO,120) BETWEEN @FechaDesde AND @FechaHasta) AND AGENDA.ID_EMPRESA = @IdEmpresa'	

	IF @IdEmpleado <> -1 BEGIN
		SET @statement = @statement + N' AND AGENDA.ID_EMPLEADO = @IdEmpleado '
	END

	IF (SELECT COUNT (*) FROM #TempServicios) > 0 BEGIN
		SET @statement = @statement + N' AND AGENDA.ID_SERVICIO IN (SELECT Id_Servicio FROM #TempServicios)'
	END	

	SET @statement = @statement + N' ORDER BY FECHA_INICIO DESC'

	EXECUTE sp_executesql @statement,
	N'@FechaDesde DATE, @FechaHasta DATE, @IdEmpresa VARCHAR(36), @IdEmpleado INT',
	@FechaDesde, @FechaHasta, @IdEmpresa, @IdEmpleado


	IF OBJECT_ID('tempdb..#TempBusquedaAgenda') IS NOT NULL DROP TABLE #TempBusquedaAgenda
	IF OBJECT_ID('tempdb..#TempServicios') IS NOT NULL DROP TABLE #TempServicios
END

GO